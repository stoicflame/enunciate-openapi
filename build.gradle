buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0'
	}
}

plugins {
	id "java"
	id "eclipse"
	id "maven-publish"
}

defaultTasks 'dist'

// Setup to use Jyske Bank Nexus if present, otherwise use Maven Central
ext {
	enunciateVersion = "2.11.1"

	junitPlatformVersion = '1.5.1'
	junitJupiterVersion = '5.5.1'
	
	jbNexus = System.getenv('NEXUS_REPO_URL')
	publicVersion = version.replaceAll("([V-])0(\\d)", '$1$2').replace("V", "").replace("-", ".")
	publishDir = file("dist/publish")
	
	jdkHome = findJdkHome()
}

if (jbNexus) {
	System.setProperty("javax.net.ssl.trustStore", file("gradle/jb-nexus-trust").getAbsolutePath());
  	System.setProperty("javax.net.ssl.trustStorePassword", "changeit"); 
}

repositories {
	if (jbNexus) {
		maven {
	    	url jbNexus
	  	}
    } else {
    	mavenCentral()
    }
}

dependencies {
    compile "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.9.9"
    compileOnly "com.webcohesion.enunciate:enunciate-core:${enunciateVersion}"
    compileOnly "com.webcohesion.enunciate:enunciate-jaxb:${enunciateVersion}"
    compileOnly "com.fasterxml.jackson.core:jackson-databind:2.9.9.1"
    compileOnly 'org.freemarker:freemarker:2.3.23'

	testCompile "org.codehaus.jackson:jackson-mapper-asl:1.9.13"
	testCompile "org.jboss.resteasy:jaxrs-api:3.0.12.Final"
	testCompile files("$jdkHome/lib/tools.jar")
	testCompile "org.slf4j:slf4j-ext:1.7.7"
	testCompile "ch.qos.logback:logback-classic:1.1.2"
    testCompile "com.webcohesion.enunciate:enunciate-top:${enunciateVersion}"
   	testCompile files("build/distributions/openapi-swagger-ui.zip")

   	testCompile "org.assertj:assertj-core:3.12.2"
	testCompile("org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}")
	testCompile("org.junit.platform:junit-platform-runner:${junitPlatformVersion}")
	testRuntime("org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}")
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"


task packSwaggerUi(type: Zip) {
     from "swagger-ui"
     archiveName = "openapi-swagger-ui.zip"
}

jar {
    from ("src/main/java") {
        include "**/*.ftl"
    }
    from packSwaggerUi
    destinationDir = publishDir
    
    manifest {
		attributes(["Implementation-Version" : publicVersion])
	}
	version = publicVersion
}


task javadocJar(type: Jar, dependsOn: javadoc) {
	from javadoc
    destinationDir = publishDir

    manifest {
		attributes(["Implementation-Version" : publicVersion])
	}
	version = publicVersion
	classifier = "javadoc"
}

task sourceJar(type: Jar) {
	from sourceSets.main.allSource
    destinationDir = publishDir

    manifest {
		attributes(["Implementation-Version" : publicVersion])
	}
	version = publicVersion
	classifier = "sources"
}

test.doFirst {
	project.copy {
       	from 'build/resources/main/META-INF'
       	into 'build/classes/main/META-INF/'
   	}
}

task addPackedSwaggerUiToEclipseTest(type: Copy){
	from packSwaggerUi
	into file("bin/test")
}

task addPackedSwaggerUiToTest(type: Copy){
	from packSwaggerUi
	into file("build/classes/java/test")
}


task deleteDist(type: Delete) {
	delete("dist")
	delete("bin")
}
tasks.clean.dependsOn(deleteDist)

group 'dk.jyskebank.tooling.enunciate'
publishing {
	publications {
		mavenJava(MavenPublication) {
			pom.withXml {
				asNode().with {
					appendNode('name', 'enunciate-openapi')
					appendNode('description', 'Enunciate module that generates OpenAPI output')
					appendNode('url', 'https://github.com/Jyskebank/enunciate-openapi')
					appendNode('licenses').with {
						appendNode('license').with {
							appendNode('name', 'The Apache Software License, Version 2.0')
						 	appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
						}
					}
					appendNode('developers').with {
						appendNode('developer').with {
							appendNode('id', 'jskov')
							appendNode('name', 'Jesper Skov')
							appendNode('email', 'jskov@jyskebank.dk')
							appendNode('organization', 'Jyske Bank')
							appendNode('organizationUrl', 'http://www.jyskebank.dk')
							appendNode('roles').with {
								appendNode('role', 'developer')
							}
							appendNode('timezone', 'Europe/Copenhagen')
						}
					}
					appendNode('scm').with {
						appendNode('connection', 'scm:git:git@github.com:Jyskebank/enunciate-openapi.git')
						appendNode('developerConnection', 'scm:git:git@github.com:Jyskebank/enunciate-openapi.git')
						appendNode('url', 'https://github.com/Jyskebank/enunciate-openapi')
					}
				}
			}
			from components.java
			version = publicVersion
		}
	}
}
model {
	tasks.generatePomFileForMavenJavaPublication {
		destination = new File(publishDir, "${project.name}-${publicVersion}.pom")
	}
}

// Prepare packaged ui for eclipse tests
tasks['eclipse'].dependsOn("addPackedSwaggerUiToEclipseTest", "cleanEclipseClasspath")
tasks['test'].dependsOn("addPackedSwaggerUiToTest")


task dist(dependsOn: ["jar", "javadocJar", "sourceJar", "generatePomFileForMavenJavaPublication"])
task eclipseIvyChanged(dependsOn: "eclipse")
task eclipseGradlePropertiesChanged(dependsOn: "eclipse")
task eclipsePropertiesFolderChanged(dependsOn: "eclipse")
task ciBuild(dependsOn: "test")

eclipseJdt.doLast {
    File f = file('.settings/org.eclipse.core.resources.prefs')
    f.write('eclipse.preferences.version=1\n')
    f.append('encoding/<project>=UTF-8')
}

File findJdkHome() {
	File home = new File(System.getProperty("java.home"))
	if (!home.list().contains("jre")) {
		home = home.getParentFile()
	}
	return home;
}